/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

buildscript {
    ext.androidx_version = '1.0.0'
    ext.architecture_components_version = '2.0.0'
    ext.kotlin_version = '1.3.30'
    ext.coroutines_version = '1.0.1'
    ext.moz_components_version = '0.49.0'

    ext.robolectric_version = '4.0.2' // required for SDK 28.

    // Support library test dependencies
    // - List of artifacts: https://developer.android.com/training/testing/set-up-project#android-test-dependencies
    // - Keep pinned to tested versions (in release notes): https://developer.android.com/jetpack/androidx/releases/archive/test
    ext.test_core = '1.1.0'
    ext.test_runner = '1.1.1'
    ext.test_rules = ext.test_runner
    ext.test_orchestrator = '1.1.1'
    ext.espresso_version = '3.1.1'
    ext.uiautomator_version = '2.2.0'
    ext.junit = "1.1.0"

    repositories {
        google()
        maven {
            url "https://maven.mozilla.org/maven2"
        }
        jcenter()
    }

    dependencies {
        // If you ever see strange Gradle behavior that looks related to the version,
        // double check that this matches build.gradle.kts
        classpath 'com.android.tools.build:gradle:3.3.2'

        classpath 'org.ajoberstar:grgit:1.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC9.2"
}

detekt {
    toolVersion = "1.0.0.RC9.2"
    input = files("$projectDir")
    config = files("$projectDir/quality/detekt.yml")
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
}

allprojects {
    repositories {
        maven {
            url "https://maven.mozilla.org/maven2"
        }
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.27.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "**/*.kt"
}
